CLASS BIG_FUCKING_GUN : doomb_weapon_base Replaces BFG9000 { 
Default {
	
	Weapon.SlotNumber 7;
	Weapon.SlotPriority 0;
	
	Height 20;
	Weapon.SelectionOrder 2800;
	Weapon.AmmoUse 40;
	Weapon.AmmoGive 40;
	Weapon.AmmoType "Cell";
	Inventory.PickupSound "BFGREADY";
	//DamageType Desintegrate
	+FORCEXYBILLBOARD;
	+WEAPON.NO_AUTO_SWITCH;
	Inventory.PickupMessage "You got the BFG-9000! FUCK YEAH!";
}
States
{
	Spawn:
		TRE2 A 0;
		BFUG A 0 Thing_ChangeTID(0,748);
	VerificateNazi:
		TNT1 A 1;
		TNT1 A 0 A_JumpIfInventory("NaziVerification2", 1, "IsANazi");
	Goto Spawn2;
		
	ISANazi:
		TNT1 A 0 A_SpawnItemEx("HitlersBuzzsaw", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS | SXF_TRANSFERAMBUSHFLAG);
	Stop;

	Spawn2:
		BFUG A -1;
	Stop;


	Select:
		BFGG A 1 A_Raise;
	wait;

	Ready:
		TNT1 A 0 A_GunFlash;
		TNT1 A 0
		{
			A_PlaySound("BFGREADY");
			//by default its 0, but gzdoom wont check it until something change it
			self.player.ReadyWeapon.FOVScale  = 1;
		}
	ready_loop:
		BFGG A 4 A_WeaponReady(WRF_ALLOWRELOAD);
	loop;

	Fire:
		TNT1 A 0 A_PlaySound("BFGCHARGE");
		BFGC ABABABABABABABAB 2 BRIGHT
		{
			A_FireProjectile("GreenFlareSpawn",0,0,0,0);
			self.player.ReadyWeapon.FOVScale  -= 0.005;
		}
		BFGF A 2 BRIGHT A_FireProjectile("GreenFlareSpawn",0,0,0,0);
		TNT1 A 0 A_PlaySound("BFGFIRE");
		TNT1 AAA 0
		{
			A_FireProjectile("GreenFlareSpawn",0,0,0,0);
		}
		BFGF B 2 BRIGHT
		{
			A_Recoil(8);
			A_FireProjectile("Alerter", 0, 0, -1, 0);
			A_FireProjectile("BFGBall");
			Radius_Quake (2, 6, 0, 1, 0);
		}
		BFGF CCDDEEFF 1 BRIGHT
		{
			A_FireProjectile("GreenFlareSpawn",0,0,0,0);
			self.player.ReadyWeapon.FOVScale  += 0.01;
		}
		BFGG A 1 BRIGHT;
		BFGG A 4;
		TNT1 A 0 A_Refire;
	Goto Ready_loop;


	Deselect:
		BFGG A 1 A_Lower;
	wait;
}

}



CLASS SuperBFGBall : BFGBall replaces BFGBall { 
Default {
	
	+EXTREMEDEATH;
	Damage 100;
	Decal "BFGLightning";
	DeathSound "BFGEXPLO";
	DamageType "Desintegrate";
	Scale 0.7;
	Species "Marines";
	+THRUSPECIES;
	+MTHRUSPECIES;
}
States
{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("BFGFOG", 0, 0);
		BFS1 A 1 BRIGHT A_SpawnItem("GreenFlareMedium",0,0);
		BFS1 A 1 BRIGHT A_SpawnItemEx("green_particle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128);
		TNT1 A 0 A_SpawnItemEx("BFGFOG", 0, 0);
		BFS1 B 1 BRIGHT A_SpawnItem("GreenFlareMedium",0,0);
		BFS1 B 1 BRIGHT A_SpawnItemEx("green_particle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128);
	Loop;
		
	Death:
		TNT1 A 0 Radius_Quake(4,34,0,12,0);
		TNT1 A 0 Bright A_SpawnItem("GreenShockWave",0,0,0);
		EXPL a 0
		{
			for(uint i = 0; i < 400; i++)
			{
				spawn("green_particle", pos:self.pos + (0, 0, height/2) ).vel = (frandom(-1, 1), frandom(-1, 1), frandom(-1, 1));
			}
			A_SpawnProjectile ("BFGBIGFOG", 0, 0, random (0, 360), 2, random (0, 360));
		}
		//add removing gib functionality here
		TNT1 A 0 A_Explode(80,300,0);
		BFGB ABCDE 1 Bright A_SpawnItem("GreenFlare",0,0);
		BFGB F 1 Bright A_BFGSpray("SuperBFGExtra", 40, 1);
		TNT1 A 0 A_SPawnItem("SuperBFGExtraGiant");
		BFGB GHIJK 1 Bright A_SpawnItem("GreenFlare",0,0);
	Stop;
}
}


class green_particle : visual_sfx
{
Default
{
    -nogravity
    bouncefactor 0.8;
	RenderStyle "Add";
	Speed 1;
    Gravity 0.8;
    scale 1;
}
	States
	{
		Spawn:
            tnt1 a 0 nodelay
            {
                vel *= random(1, 24);
                scale *= frandom(0.035, 0.1);
            }

            SPKG A 1 Bright
            {
                A_FadeOut(0.01);
				vel += (frandom(1,-1), frandom(1,-1), frandom(1,-1) );
           }
        wait;
	}

}



CLASS FriendlyBFGBAll : SuperBFGBall { 
Default {
	
	Damagetype "FriendBullet";
	+EXTREMEDEATH;
}
States
{
	Death:
		TNT1 A 0;
		//TNT1 A 0 Radius_Quake(4,34,0,12,0)
		
		TNT1 A 0 Bright A_SpawnItem("GreenShockWave",0,0,0);
		EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile ("BFGBIGFOG", 0, 0, random (0, 360), 2, random (0, 360));
		EXPL a 0
		{
			for(uint i = 0; i < 100; i++)
			{
				spawn("green_particle", pos:self.pos + (0, 0, height/2) ).vel = (frandom(-1, 1), frandom(-1, 1), frandom(-1, 1));
			}
		}
		//add removing gib functionality here
		TNT1 A 0 A_Explode(120,350,0);
		BFGB ABCDE 1 Bright A_SpawnItem("GreenFlare",0,0);
		TNT1 A 0 A_SPawnItem("SuperBFGExtraGiant");
		BFGB GHIJK 1 Bright A_SpawnItem("GreenFlare",0,0);
		Stop;
}
}





CLASS SuperBFGExtra : actor Replaces BFGExtra { 
Default {
	+NoBlockMap;
	+NoGravity;
	RenderStyle "Add";
	+FORCERADIUSDMG;
	+THRUGHOST;
	Alpha 0.75;
	Damage 0;
	Scale 0.6;
	Radius 1;
	Height 1;
	DamageType "Desintegrate";
}
States
{
	Spawn:
		BFE2 A 0;
		TNT1 A 0 A_Explode(40, 3);
		BFGB ABCDEFGHIJK 2 Bright A_SpawnItem("GreenFlareMedium",0,0);
		stop;
}
}


CLASS SuperBFGExtraGiant : SuperBFGExtra { 
Default {
	
	Scale 2.9;
}
States
{
	Spawn:
		BFGB ABCDEFGHIJK 2 Bright A_SpawnItem("GreenFlareMedium",0,0);
		Stop;
		
}

}


CLASS GreenShockWave  : ACTOR { 
Default {
	
	Speed 0;
	Height 64;
	Radius 32;
	Scale 2.25;
	RenderStyle "Add";
	Alpha 0.9;
	+DROPOFF;
	+NOBLOCKMAP;
	+NOGRAVITY;
	+CLIENTSIDEONLY;
}
States 
{ 
	Spawn: 
		SHOK ABCDEFGHIJKLMNOPQR 1 BRIGHT A_FadeOut(0.05);
		Stop;
} 
} 


CLASS WhiteShockWaveBig: GreenShockwave { 
Default {
	
	Scale 3.0;
}
States 
{ 
	Spawn: 
		SHWK ACEGIKMOQ 1 A_FadeOut(0.01);
		Stop;
} 
} 

CLASS WhiteShockWaveSmall: GreenShockwave { 
Default {
	
	Scale 1.0;
}
States 
{ 
	Spawn: 
		SHWK ACEGIKMOQ 1 A_FadeOut(0.01);
		Stop;
} 
} 

CLASS BFGFOG : ACTOR { 
Default {
	
	Radius 1;
	Height 1;
	Alpha 0.7;
	RenderStyle "Add";
	Scale 0.4;
	Speed 8;
	Gravity 0;
	+NOBLOCKMAP;
	+NOTELEPORT;
	+DONTSPLASH;
	+MISSILE;
	+FORCEXYBILLBOARD;
	+CLIENTSIDEONLY;
}
States
{
	Spawn:
		BFOG ABCDEFG 6 BRIGHT A_SpawnItem("GreenFlareMedium",0,0);
		Stop;
}
}

CLASS BFGBIGFOG: BFGFOG { 
Default {
	
	Scale 0.8;
}
}

CLASS SmallGreenFog: BFGFOG { 
Default {
	
	Speed 3;
	Scale 0.5;
}
}


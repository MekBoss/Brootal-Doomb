
// --------------------------------------------------------------------------
//
// Rocket launcher
//
// --------------------------------------------------------------------------

CLASS Rocket_Launcher : doomb_weapon_base Replaces RocketLauncher { 
	Default {
		
		Weapon.SlotNumber 5;
		Weapon.SlotPriority 0;
		
		Speed 20;
		Damage 20;
		Weapon.SelectionOrder 9500;
		Weapon.AmmoUse1 0;
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		Weapon.AmmoGive1 6;
		Weapon.AmmoType1 "doomb_rockets_ammo";
		Weapon.AmmoType2 "RocketRounds";
		Inventory.PickupSound "ROCKPKUP";
		+WEAPON.NOAUTOAIM;
		+FORCEXYBILLBOARD;
		+WEAPON.AXEBLOOD;
		+WEAPON.NO_AUTO_SWITCH;
		Inventory.PickupMessage "$GOTLAUNCHER";
	}
	States
	{
		Spawn:
			LAUN A -1 ;
		Stop;

		Select:
			MISG A 0 A_Raise;
		Wait;

		Ready:
			TNT1 A 8 A_PlaySound("RLANDRAW");
		ready_loop:
			MISG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
			
			
		Fire:
			TNT1 A 0 A_JumpIfInventory("RocketRounds",1,1);
		Goto Reload;
			MISF A 1 BRIGHT
			{
				A_FireProjectile("Rocket", 0, 1, 0, -10);
				A_FireProjectile("RedFlareSpawn",-5,0,0,0);
				A_PlaySound ("DSRFIRE");
				A_Takeinventory("RocketRounds",1);
			}
			MISF BCD 1 BRIGHT;
			MISF E 1 A_SetPitch(-6.0 + pitch);
			MISG BCD 2 A_SetPitch(2.0 + pitch);
			MISG EF 2;
			TNT1 A 0 A_Refire;
		Goto Ready_loop;
			
			
		Reload:
			MISG A 1 A_WeaponReady;
			MISG A 6;

			TNT1 A 0 A_JumpIfInventory("RocketRounds",6,"NoNeedToReload");
			TNT1 A 0 A_JumpIfInventory("doomb_rockets_ammo",1,1);
		Goto Ready_loop;
			TNT1 A 0 A_PlaySound("Reload");
			TNT1 A 0 A_SetAngle(-2 + angle);
			MISR ABCDDE 1;
			TNT1 A 0 A_FireProjectile("RocketCaseSpawn",-30,0,-4,-4);
			MISR EFFGG 1;
			TNT1 A 0 A_SetAngle(+2 + angle);
			MISR HHIIJJKLM 1;
			TNT1 A 0 A_SetAngle(+1 + angle);
			MISR MMMNOP 1;
			TNT1 A 0 A_SetAngle(-1 + angle);
			MISR Q 3;
			MISR PR 1;
			MISG DEF 2;
			MISG A 6 {invoker.reload_weapon_magazine("RocketRounds", "doomb_rockets_ammo");}
		NoNeedToReload:
			MISG A 1 A_Refire;
		Goto Ready_loop;


		Deselect:
			MISS GFEDCBA 1;
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower;
		Wait;

	}
}


CLASS Rocket2 : doomb_boolet_base Replaces Rocket
{

Default
{	
	Radius 4;
	Height 2;
	Projectile;
	Speed 35;
	Damage 30;
	Scale 0.7;
	Decal "Scorch";
	+EXTREMEDEATH;
	+BLOODSPLATTER;
	SeeSound "weapons/RLL";
	DeathSound "Explosion";
	Obituary "$OB_MPROCKET";
	Species "Marines";
}

States
{
	Spawn:
		MISL A 1 Bright;
		TNT1 A 0 A_PlaySound("DSRLAUN");
		TNT1 A 0 A_JumpIf(waterlevel > 1, "SpawnUnderwater");
		TNT1 A 0 A_SpawnItem("RocketFlare",-20,0);
		TNT1 A 0 doomb_show_effect("ExplosionSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ), (0.3,0), 0.3 );
		Loop;
		
	SpawnUnderwater:
		TNT1 A 0 {bNOGRAVITY = false;}
		TNT1 A 0 A_SpawnItem("RocketFlare",-20,0);
		TNT1 A 0 A_SpawnProjectile ("BUBULZ", 0, 0, random (0, 360), 2, random (0, 180));
	Goto Spawn;
		
	Death:
		EXPL A 0 Radius_Quake (3, 8, 0, 15, 0);//(intensity, duration, damrad, tremrad, tid)
		TNT1 A 0 A_SpawnProjectile("BigRicoChet");
		TNT1 A 0 {if(waterlevel > 1) spawn("UnderwaterExplosion", pos:self.pos); }
		TNT1 A 0 A_SpawnItemEx ("NewfagExplosionFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
		TNT1 a 0
		{
			for(uint i = 0; i < 50; i++)
			{
				doomb_show_effect("ExplosionParticle2", (0, 0, 0), (frandom(-10, 10), frandom(-10, 10), frandom(-10, 10) ) );
			}
		}
		TNT1 A 0 doomb_show_effect("ExplosionFlames", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5) ), (frandom(0.9, 1.4), 0) );
		SHWK A 1
		{
			A_Explode(120,150, damagetype:"Explosive_Impact");
			A_Explode(3,9, damagetype: "CauseWaterSplash");
			A_PlaySound("FAREXPL", 3);
		}
		SHWK CEGIKMOQ 1 A_FadeOut(0.01);
	Stop;
}
}

CLASS RocketRounds : Ammo { 
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 6;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 6;
		Inventory.Icon "LAUNA0";
	}
}

CLASS RocketCaseSpawn : ACTOR { 
	Default {
		Speed 20;
		PROJECTILE;
		+NOCLIP;
		+CLIENTSIDEONLY;
	}
	States
	{
		Spawn:
			TNT1 A 0;
			TNT1 A 1 A_SpawnProjectile("EmptyRocketChamber",0,0,random(-80,-100),2,random(40,60));
			Stop;
	}
} 


CLASS EmptyRocketChamber : ACTOR { 
	Default {
		
		Height 2;
		Radius 2;
		Speed 4;
		Scale 1.3;
		+DOOMBOUNCE;
		- NOGRAVITY;
		+WINDTHRUST;
		+CLIENTSIDEONLY;
		+MOVEWITHSECTOR;
		+MISSILE;
		+NOBLOCKMAP;
		-DROPOFF;
		+NOTELEPORT;
		+FORCEXYBILLBOARD;
		+NOTDMATCH;
		+GHOST;
		Mass 1;
		SeeSound "weapons/barrelpain";
	}
	States
	{
		Spawn:
			TNT1 A 7 A_PlaySound("tnt1");
			RCSE ABCDEFGHABCDEFGHABCDEFGHABCDEFGHABCDEFGH 4;
			Stop;
		Death:
			RCSE G 600;
			Loop;
	}
}


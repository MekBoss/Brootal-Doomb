CLASS doomb_Marine_RocketL: doomb_marine_base
{ 
	Default {
		doomb_marine_base.spare_ammo "doomb_rockets_ammo";
		
		DropItem "RocketLauncher";
	}

override int doomb_get_aditional_death_sprite()
{
	return GetSpriteIndex("PL5S");
}

	States
	{
			
		Spawn:
			tnt1 a 0
			{
				if(is_guarding() )
					setstatelabel("guard_see");
			}
			PL5S A 1
			{
				A_Look();
				doomb_set_leader();
			}

		See:
			tnt1 a 0
			{
				if(is_guarding() )
					setstatelabel("guard_see");
			}
			PL5S AABBCCDD 1
			{
				doomb_marine_chase("rl_Melee", "missile", "rl_on_guard");
			}
		loop;

		rl_on_guard:
			PL5S E 1 A_Stop();
			PL5S EEEE 10
			{
				a_look();
				if(doomb_sqrd_distance_to_leader() > 300 * 300)
					setstatelabel("see");

				//no sprites
				//doomb_try_share_ammo();
			}
			tnt1 a 0
			{
				angle += randompick(-40, -80, 40, 80);
			
				if(random(0, 10) <= 1)
					setstatelabel("wander_around");
			}
		loop;

		wander_around:
			PL5S E 1;
			tnt1 a 0
			{
				if(leader)
					a_face(leader);
				
				angle += random(-40, 40);
			}
			PL5S AABBCCDDAABBCCDD 1
			{
				vel.xy = (10 * cos(angle), 10 * sin(angle) );
			}
			tnt1 a 0 {vel = (0, 0, 0);}
		goto rl_on_guard;
			
		share_some_ammo:
			PL5S A 10 A_FaceTarget();
			TNT1 A 0 A_PlaySound("MRNGI1");
			PL5S I 30 doomb_give_leader_ammo();
		goto rl_on_guard;


		Missile:
			tnt1 a 0
			{
				if(is_guarding() )
					setstatelabel("guard_fire");

				bnodropoff = true;

				doomb_trigger_monsters(cursector);
				doomb_if_boss_retreat(target, "CheckRetreat");
				A_PlaySound("MRNAT", 2);
			}
		Missile2:
			PL5S EEE 5 A_FaceTarget;
			TNT1 A 0 A_PlaySound ("DSRFIRE");
			PL5S E 2 BRIGHT A_SpawnProjectile("Rocket",50,0,0,0);
			PL5S EEE 5 A_FaceTarget;
		Goto See;
			
			
			
		rl_Melee:
			TNT1 A 0;
			PLAM A 4 A_FaceTarget;
			TNT1 A 0 A_Recoil(-4);
			PLAM A 1 A_FaceTarget;
			PLAM B 4 doomb_marine_punch(80, random(20, 60), 'marine_punch');
			PLAM B 2;
		Goto See;
			
		Pain:
			PL5S G 3 {bnodropoff = false;}
			PL5S G 3 A_Pain;
			TNT1 A 0 A_Jump(128, "Avoid");
			tnt1 a 0
			{
				if(is_guarding() )
					setstatelabel("guard_fire");
			}
		Goto see;

		CheckRetreat:
			TNT1 A 0 A_JumpIfCloser(500, "Retreat");
		Avoid:
			PL5S ABCD 3
			{
				A_FaceTarget();
				int i = randompick(90, -90);
				vel.xy += (3 * cos(angle + i), 3 * sin(angle + i) );
			}
			tnt1 a 0
			{
				if(is_guarding() )
					setstatelabel("guard_fire");
			}
		Goto Missile2;
			
		Retreat:
			PL5S ABCD 3
			{
				A_FaceTarget();
				A_Recoil(5);
			}
		Goto MIssile2;











		follow_player:
			TNT1 A 0 A_PlaySound("MarineFollow", 2);
			TNT1 A 0 A_SpawnProjectile ("OrderTitle1", 50, 0, 0, 2, -90);
			PL5S A 3 {bnoclip = false;}
		goto see;

		guard_area:
			TNT1 A 0 A_PlaySound("MarineGuard", 2);
			TNT1 A 0 A_SpawnProjectile ("OrderTitle2", 50, 0, 0, 2, -90);
			PL5S A 3;
			TNT1 A 0 {bnoclip = true;}
			PL5S E 1 A_Look;
		guard_see:
			PL5S E 1 A_Chase(null, "guard_fire");
		Loop;

		guard_fire:
			TNT1 A 0 doomb_trigger_monsters(cursector);
			TNT1 A 0 doomb_if_boss_retreat(target, "CheckRetreat");
			PL5S EEE 5 A_FaceTarget;
			TNT1 A 0 A_PlaySound ("DSRFIRE");
			PL5S E 2 BRIGHT A_SpawnProjectile("Rocket",50,0,0,0);
			PL5S EEE 5 A_FaceTarget;
		goto guard_see;
	}
}
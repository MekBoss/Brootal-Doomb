

CLASS NumberOfAlliesMinigun : Inventory { 
	Default {
		
		inventory.maxamount 5;
	}
}

CLASS MarinesMinigunDispenser : Inventory { 
	Default {
		
		inventory.maxamount 5;
	}
}




CLASS doomb_Marine_Minigun: doomb_marine_base Replaces MarineChaingun
{ 
Default {
	doomb_marine_base.spare_ammo "doomb_clip";
	
	Scale 1.05;
	DropItem "MiniGun";
}

override int doomb_get_aditional_death_sprite()
{
	return GetSpriteIndex("PL42");
}

States
{
	Spawn:
		tnt1 a 0
		{
			if(is_guarding() )
				setstatelabel("guard_look");
		}
		TNT1 A 0 A_GiveInventory("TargetIsAMarine");
		PL42 A 1
		{
			A_Look();
			doomb_set_leader();
		}
	Goto See;
		
	See:
		tnt1 a 0
		{
			if(is_guarding() )
				setstatelabel("see_guard");
		}
		PL42 AABBCCDD 1
		{
			
			if(target)
			{
				vector3 vec_dist = pos - target.pos;

				if(vec_dist dot vec_dist > 100 * 100)
				{
					A_Chase(null, "missile");
					A_Recoil(-1);
				}

				else
					setstatelabel("Missile");
			}

			if(!target)
			{
				A_Chase(null, "missile");
				A_Recoil(-1);

				if(doomb_sqrd_distance_to_leader() < 200 * 200)
					setstatelabel("minigunner_on_guard");
			
				else if(doomb_sqrd_distance_to_leader() > 1000 * 1000)
					doomb_warp_to_leader();
			}
		}
	Goto See;

	minigunner_on_guard:
		PL42 H 1 A_Stop();
		PL42 HEHE 10
		{
			a_look();
			if(doomb_sqrd_distance_to_leader() > 300 * 300)
				setstatelabel("see");
		
			doomb_try_share_ammo();
		}
		tnt1 a 0
		{
			angle += randompick(-40, -80, 40, 80);
		
			if(random(0, 10) <= 1)
				setstatelabel("wander_around");
		}
	loop;

	share_some_ammo:
		PL42 A 10 A_FaceTarget();
		TNT1 A 0 A_PlaySound("MRNGI1");
		PL42 I 30 doomb_give_leader_ammo();
	Goto minigunner_on_guard;


	wander_around:
		tnt1 a 0
		{
			if(leader)
				a_face(leader);
			
			angle += random(-40, 40);
		}
		PL42 AABBCCDDAABBCCDD 1
		{
			vel.xy = (10 * cos(angle), 10 * sin(angle) );
		}
	goto minigunner_on_guard;


	Missile:
		TNT1 A 0
		{
			if(is_guarding() )
				setstatelabel("guard_fire");

			bnodropoff = true;

			doomb_trigger_monsters(cursector);
			doomb_if_boss_retreat(target, "CheckRetreat");
			bshootable = true;
			bsolid = true;
			A_PlaySound("MRNAT", 2);
		}
	Missile2:
		PL42 E 2
		{
		}
		PL42 F 1 BRIGHT
		{
			A_FaceTarget();
			A_PlaySound("CHGNSHOT", 1);
			A_SpawnProjectile("MarineStrongTracer", 32, 0, random(-8, 8), CMF_OFFSETPITCH, frandom(-3, 3) );
			A_SpawnItem ("RifleCaseSpawn", 0, 30,0);
		}
		tnt1 a 0 a_jump(30, "see");
	loop;
		
	Pain:
		PL42 G 3 {bnodropoff = false;}
		PL42 G 3 A_Pain;
		TNT1 A 0 A_Jump(128, "Avoid");
		tnt1 a 0
		{
			if(is_guarding() )
				setstatelabel("guard_fire");
		}
	Goto see;

	CheckRetreat:
		TNT1 A 0 A_JumpIfCloser(500, "Retreat");
	Avoid:
		PL42 ABCD 3
		{
			A_FaceTarget();
			int i = randompick(90, -90);
			vel.xy += (3 * sin(angle + i), 3 * cos(angle + i) );
		}
		tnt1 a 0
		{
			if(is_guarding() )
				setstatelabel("guard_fire");
		}
	Goto Missile2;
		
	Retreat:
		PL42 ABCD 3
		{
			A_FaceTarget();
			A_Recoil(5);
		}
	Goto MIssile2;
		
		
		











	guard_area:
		TNT1 A 0 {bnoclip = true;}
		TNT1 A 0 A_PlaySound("MarineGuard", 2);
		TNT1 A 0 A_SpawnProjectile ("OrderTitle2", 50, 0, 0, 2, -90);
	guard_look:
		PL42 EEEEEEEEEEHHHHHHHHH 1 A_LookEx(label:"guard_fire");
		tnt1 a 0 {angle += random(-80, 80);}
	loop;


	see_guard:
		PL42 EEEEEEEEEEHHHHHHHHH 1 A_Chase(null, "guard_fire");
	loop;

	guard_fire:
		PL42 E 2
		{
			A_FaceTarget();
			bshootable = true;
			bsolid = true;
		}
		PL42 F 1 BRIGHT
		{
			A_PlaySound("CHGNSHOT", 1);
			A_SpawnProjectile("MarineStrongTracer", 32, 0, random(-8, 8), CMF_OFFSETPITCH, frandom(-3, 3) );
			A_SpawnItem ("RifleCaseSpawn", 0, 30,0);
		}
		tnt1 a 0 a_monsterrefire(200, "see_guard");
	Goto guard_fire;


	follow_player:
		TNT1 A 0 A_PlaySound("MarineFollow", 2);
		TNT1 A 0 A_SpawnProjectile ("OrderTitle1", 50, 0, 0, 2, -90);
		PL42 A 3 {bnoclip = false;}
	goto see;

}

}
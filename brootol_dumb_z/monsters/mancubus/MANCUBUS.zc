CLASS doomb_fatso : doomb_monster_base
{ 
	Default {
		
		doomb_monster_base.headshot_multiplier 2;
		doomb_monster_base.head_starting_position 58;//so that if someone forgot to define this number monster dont get headshot from every attack
		doomb_monster_base.legs_height_max -1;//same for legs
		
		speed 8;
		Scale 1.2;
		MaxStepHeight 16;
		Radius 48;
		height 64;
		GibHealth 30;
		Health 500;
		Mass 500;
		MaxDropOffHeight 24;
		PainChance 80;

		damagefactor "Crush", 10.0;
		DamageFactor "SSG", 0.1;
		DamageFactor "Shotgun", 0.8;

		PainChance "Kick", 255;
		PainChance "SSG", 255;
		PainChance "Taunt", 255;
		PainChance "Explosive_Impact", 55;

		+FLOORCLIP
		+DONTHARMCLASS;
		+THRUGHOST;
		+BOSSDEATH
		Monster;

		SeeSound "fatso/sight";
		PainSound "fatso/pain";
		DeathSound "fatso/death";
		ActiveSound "fatso/active";

		Obituary "%o was incinerated by a mancubus.";
		Tag "Mancubus";
		BloodType "Brutal_Blood",  "SawBlood";
		DropItem "DemonStrengthRune", 5;
		species 'fatso';
	}
	States
	{
		Spawn:
			FATT B 10 A_Look;
		Loop;
			
		Idle:
		LookingForPlayer:
			FATT BBB 10 A_Look;
			TNT1 A 0 A_SetAngle(angle - 40);
			FATT BBB 10 A_Look;
			TNT1 A 0 A_SetAngle(angle + 40);
			
			FATT BBB 10 A_Look;

			FATT AABBCCDD 2
			{
				A_Look();
				A_Wander();
			}
		loop;
			
			
			
		ForgetTarget:
			TNT1 A 0 A_ClearTarget;
		Goto LookingForPlayer;
			
		See:
			TNT1 A 0;
		LookingForPlayer:
			FATT AABBCCDDEEFF 4 A_Chase;
			TNT1 A 0 A_JumpIfInTargetInventory("IsInvisible", 1, "ForgetTarget");
			tnt1 a 0 doomb_monster_memory();
		Goto See;
			
		Missile:
			TNT1 A 0 doomb_update_memory();
			TNT1 A 0 A_JumpIfCloser(300, "Melee");
			FATT G 4 A_FaceTarget;
			TNT1 A 0 A_JumpIfInventory("Enraged", 1, 1);
			FATT G 6 A_FatRaise;
			FATT G 10 A_FaceTarget;
		actual_fire:
			FATT IG 5 A_FaceTarget;
			FATT H 10 BRIGHT
			{
				A_SpawnProjectile("BigFireBall",30,-25,0,0);
				A_SpawnProjectile("BigFireBall",30,25,-10,0);
			}
			tnt1 a 0 a_jump(200, "actual_fire");
		Goto See;
			
		Melee:
			TNT1 A 0 doomb_update_memory();
			TNT1 A 0 A_JumpIfInventory("Enraged", 1, 1);
			FATT G 10 A_FatRaise;
			FATT G 10 A_FaceTarget;
			TNT1 A 0 A_PlaySound("BREATHOF");
			TNT1 A 0 A_SpawnProjectile("FlamethrowerMissile",30,-25,15,0);
			TNT1 A 0 A_SpawnProjectile("FlamethrowerMissile",30,25,-15,0);
			FATT H 10 BRIGHT;
			FATT IG 5 A_FaceTarget;
			tnt1 a 0 a_jump(128, "melee");
		Goto See;

		Pain:
			FATT J 3;
			FATT J 3 A_Pain;
		Goto See;
			
		Pain.Explosive_Impact:
		Pain.SSG:
			TNT1 A 0 A_FaceTarget;
			TNT1 A 0 {bNOPAIN = true;}
			FAT4 A 4 {vel.z += 5;}
			FAT4 B 4 A_Pain;
			FAT4 CDDDD 4;
			FAT4 C 4 A_Stop;
			FAT4 BA 4;
			TNT1 A 0 {bNOPAIN = false;}
		Goto See;
			
		Pain.Kick:
			TNT1 A 0 A_FaceTarget;
			TNT1 A 0 A_Recoil(2);
			FATT J 9 A_Pain ;
			FATT J 8;
		Goto See;
			
		Death.Headshot:
			TNT1 A 0 A_Jump(126, "headshot_1");
			tnt1 a 0 A_Explode(3,200, 0, 0, 0, 0, 0, "none", "blood");
			TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
			TNT1 AA 0 doomb_severed_limb("jaw", "spawn", (0, 0, 62), doomb_random_velocity(7) );
			TNT1 AA 0 doomb_severed_limb("head_piece_dark", "spawn", (0, 0, 62), doomb_random_velocity(7) );
			TNT1 AA 0 doomb_severed_limb("doomb_fatso_Eyeballs", "spawn", (0, 0, 62), doomb_random_velocity(7) );
			FATT KKKLLL 2 doomb_for_khorne("doomb_non_interactive_blood", "red_blood_trail", (0, 0, random(20, 30) ), (frandom(-5, 5), frandom(-5, 5), frandom(0, 5) ) );
			TNT1 A 0 A_Screamandunblock;
			FATT MMMNNOOPPQQRRSSTTTTTTTTTTTTTTTTTTTTTTT 2 doomb_for_khorne("doomb_non_interactive_blood", "red_blood_trail", (0, 0, random(20, 30) ), (frandom(-5, 5), frandom(-5, 5), frandom(0, 5) ) );
			TNT1 A 0 doomb_spawn_bloodpool();
			TNT1 A 0 doomb_severed_limb("DeadFatNoHead", limb_ignore_cvar: true);
		Stop;
			
		headshot_1:
			TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
			TNT1 A 0 doomb_severed_limb("Xdeathdoomb_fatsoHead", "spawn", (0, 0, 62), doomb_random_velocity(7) );
			FATT O 6 A_Scream;
			FATT P 6 A_NoBlocking;
			FATT QRS 6;
			FATT T 1 A_BossDeath;
			TNT1 A 0 doomb_spawn_bloodpool();
			TNT1 A 0 doomb_severed_limb("DeadFatNoHead", limb_ignore_cvar: true);
		Stop;
			
			
			
		Death:
			FAT3 A 1 A_Jump(64, "Death2");
			FAT3 B 6 A_Scream;
			FAT3 C 6 A_NoBlocking;
			FAT3 DEF 6;
			FAT3 F 1 A_BossDeath;
			FAT3 F -1 doomb_final_absolution(12, 12);
		Stop;
			
		Death2:
			FATK A 6;
			FATK B 6 A_Scream;
			FATK C 6 A_NoBlocking;
			FATK DEF 6;
			FATK F 1 A_BossDeath;
			TNT1 A 0 doomb_severed_limb("DeadFat", limb_ignore_cvar: true);
		Stop;
			
			
		Death.cut:
		Death.Saw:
		Death.mini_gun:
			TNT1 O 0 A_Jump(195, 1);
		Goto Death;
			TNT1 A 0 doomb_much_blood(35);
			TNT1 A 0 A_Scream;
			TNT1 O 0 A_NoBlocking;
			FAT2 KL 5 doomb_much_blood(75);
			TNT1 A 0 doomb_Rip_Guts();
			TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
			TNT1 A 0 A_BossDeath;
			FAT2 MNOP 5 doomb_much_blood(15);
			TNT1 A 0 doomb_spawn_bloodpool();
			TNT1 A 0 doomb_severed_limb("NoGutsFatso", limb_ignore_cvar: true);
		Stop;
			
		Death.Plasma:
		Death.Plasma2:
			TNT1 A 0 A_BossDeath;
			TNT1 A 0 A_Scream;
			TNT1 A 0 A_NoBlocking;
			CRB7 G 5 BRIGHT;
			TNT1 AAA 0 doomb_for_khorne("doomb_non_interactive_blood", "red_blood_trail", (0, 0, random(20, 30) ), (frandom(-5, 5), frandom(-5, 5), frandom(0, 5) ) );
			TNT1 AA 0 doomb_spawn_burned_flesh;
			CRB7 H 5 BRIGHT;
			EXPL AAAAAA 0 doomb_show_effect("ExplosionSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 1) ) );
			CRB7 IJ 5 BRIGHT;
			CARB LLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLL 10 doomb_show_effect("AltSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ), (1.5, 0) );
			CARB L -1;
			Stop;
			
		Death.Fire:
			FATK A 4;
			FATK B 4 A_Scream;
			FATK C 4 A_NoBlocking;
			FATK DEF 4;
			FATK F 1 A_BossDeath;
			TNT1 A 0 A_Playsound("props/redfire");
			CARB LLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLL 2 doomb_show_effect("FlameTrails", (0, 0, 32), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ) );
			CARB LLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLL 10 doomb_show_effect("ExplosionSmoke", (0, 0, 32), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ), (0.5, 0), 0.1 );
			CARB L -1;
		Stop;

		XDeath:
		Death.Explosive_Impact:
			TNT1 A 0 doomb_severed_limb("MeatDeath");
			TNT1 AAAAAAAAAAAAAA 0
			{
				actor a = doomb_for_khorne("doomb_non_interactive_blood", "blood_impact_splash", (random(-20, 20), random(-20, 20), random(20, 30) ) );
				if(a)
					a.scale = (1.4, 1.4);
			}
			TNT1 A 0 doomb_severed_limb("XDeathdoomb_fatsoArm", "spawn", (0, 0, 20), doomb_random_velocity(7) );
			TNT1 A 0 doomb_severed_limb("Xdeathdoomb_fatsoHead", "spawn", (0, 0, 20), doomb_random_velocity(7) );
			TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
			TNT1 A 0 doomb_much_blood();
			XFBT A 6 A_Scream;
			XFBT B 6 A_NoBlocking;
			XFBT CDEF 6 A_BossDeath;
			XFBT F 1;
			TNT1 A 0 doomb_spawn_bloodpool();
			TNT1 A 0 doomb_severed_limb("XDeadFat", limb_ignore_cvar: true);
		Stop;
			
		Crush:
			TNT1 A 0 A_PlaySound("misc/xdeath4");
			CRS3 A -1;
		Stop;
			
		Pain.Taunt:
			SPOS B 0 A_Pain;
			TNT1 A 0 {bNOPAIN = true;}
			TNT1 A 0 {bMISSILEMORE = true;}
			TNT1 A 0 {bMISSILEEVENMORE = true;}
			TNT1 A 0 A_PlaySound("demon/pain");
			FATR A 1 A_FaceTarget;
			FATR A 10 A_Pain;
			FATR BAC 8;
			TNT1 A 0 A_Pain;
			FATR ABAC 8;
			TNT1 A 0 A_Pain;
			FATR ABAC 8;
			TNT1 A 0 A_GiveInventory("Enraged", 1);
		Goto Missile;



		Death.Fatality:
			tnt1 A 0
			{
				doomb_monster_fatality_camera(target);
				A_Pain();
				A_NoBlocking();
				A_BossDeath();
			}
			FATF A 6;
			tnt1 A 0 ACS_Execute(585, 0, 0, 0, 0);
			tnt1 A 0 doomb_for_khorne("brutal_Blood", "spawn", (0, 0, 45), doomb_random_velocity(7) );
			tnt1 AAAAAAA 0 doomb_for_khorne("doomb_non_interactive_blood", "red_blood_trail", (0, 0, 45 ), (frandom(-5, 5), frandom(-5, 5), frandom(0, 5) ) );
			FATF A 6 A_PlaySound("misc/xdeath4");
			tnt1 A 0 doomb_for_khorne("brutal_Blood", "spawn", (0, 0, 45), doomb_random_velocity(7) );
			FATF A 6 A_PlaySound("fatso/pain");
			tnt1 A 0 doomb_for_khorne("brutal_Blood", "spawn", (0, 0, 45), doomb_random_velocity(7) );
			
			tnt1 a 0 Radius_Quake (4, 24, 0, 15, 0);
			tnt1 AAAAAA 0 doomb_sticky_guro(SGT_BLOOD);
			TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
			tnt1 AAAAAAAAAAA 0 doomb_for_khorne("PlayerFlyingBlood", "spawn", (0, 0, 45), doomb_random_velocity(7) );
			tnt1 A 0 ACS_Execute(580, 0, 0, 0, 0);
			tnt1 A 0 ACS_Execute(581, 0, 0, 0, 0);
			tnt1 A 0 ACS_Execute(582, 0, 0, 0, 0);
			tnt1 A 0 ACS_Execute(583, 0, 0, 0, 0);
			TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
			FATF BCD 8;
			tnt1 A 0 A_PlaySound("fatso/pain");
			FATF D 50;
	
			tnt1 A 0 
			{
				A_Givetotarget("HealthBonus",15);
				doomb_severed_limb("FatalizedFatso", limb_ignore_cvar: true);
				doomb_destroy_monster_fatality_camera();
			}
		stop;
} 

} 

CLASS BigFireBall : doomb_boolet_base
{
	Default {
		Radius 6;
		Height 8;
		Speed 20;
		Fastspeed 25;
		Damagefunction (random(25,40));
		Projectile;
		Scale 1.0;
		+FORCEXYBILLBOARD;
		+THRUGHOST;
		RenderStyle "Translucent";
		DamageType "Fire";
		ExplosionRadius 150;
		ExplosionDamage 8;
		Alpha 1;
		Decal "Scorch";
		SeeSound "BREATHOF";
		DeathSound "fatso/shotx";
	}
	States
	{
		Spawn:
			MANF AB 1 BRIGHT light ("IMPBALL")
			{
				doomb_handle_light_def('one', 'IMPBALL');
				if(frame == 1)
				{
					{doomb_flare_base.doomb_attach_flare(self, "RedFlare22");}
					actor a = doomb_show_effect("FlameTrails", (prev-pos), (0, 0, frandom(0, 1) ), (0.5, 0) );
					if(a)
					{
						a.setstate(a.curstate+2);
						//a.sprite = self.sprite;
						//a.frame = self.frame;
					}
					doomb_show_effect("ExplosionSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ), (0.3,0), 0.3 );
				}
			}
		Loop;
			
		Death:
			TNT1 A 0 A_Explode(10, 50, 0);
			TNT1 A 0 {doomb_flare_base.doomb_attach_flare(self, "NewfagExplosionFlare");}
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 doomb_show_effect("ExplosionParticle2", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ) );
			FRFX ZZZ 0 BRIGHT doomb_show_effect("BigBlackSmoke", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ) );
			TNT1 AAA 0 doomb_show_effect("TinyBurningPiece", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ) );
		burning_loop:
			FIR3 ABCDEFGHIJKLMNOP 1 bright
			{
				doomb_handle_light_def('one', 'IMPBALL');
			
				doomb_show_effect("BigBlackSmoke", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ), visual_size: (frandom(0.2, 0.4), 0) );
				doomb_show_effect("TinyBurningPiece", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ) );
				A_Explode(1, 90);
			}
		stop_burning:
			TNT1 A 0 doomb_show_effect("BigBlackSmoke", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ), visual_size: (frandom(0.2, 0.4), 0) );
			TNT1 AAA 6 doomb_show_effect("TinyBurningPiece", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ) );
			TNT1 A 0 A_Explode(10, 90);
			TNT1 A 0 doomb_show_effect("BigBlackSmoke", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ), visual_size: (frandom(0.2, 0.4), 0) );
			TNT1 AAA 8 doomb_show_effect("TinyBurningPiece", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ) );
			TNT1 A 0 A_Explode(5, 90);
			TNT1 BBBBBBBBB 16 doomb_show_effect("BigBlackSmoke", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ), visual_size: (frandom(0.2, 0.4), 0) );
		Stop;
	}
}



CLASS FlamethrowerMissile : doomb_boolet_base
{ 
	Default
	{
		Radius 2;
		Height 2;
		Speed 25;
		Damage 1;
		+NOBLOCKMAP;
		+NOTELEPORT;
		+DONTSPLASH;
		+MISSILE;
		+FORCEXYBILLBOARD;
		Damage 1;
		-RIPPER;
		+NOBLOOD;
		+NOBLOODDECALS;
		+BLOODLESSIMPACT;
		-BLOODSPLATTER;
		RenderStyle "Add";
		DamageType "fire";
		Scale 0.5;
		Alpha 1.0;
		Gravity 0;
	}
	States
	{
		Spawn:
			TNT1 A 0;
			FIR3 ABDECGHI 1 BRIGHT light ("BIGREDTORCH128")
			{
				{doomb_flare_base.doomb_attach_flare(self, "RedFlare", visual_size: 0.15);}
				doomb_show_effect("TinyBurningPiece", velocity:(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 2) ) );
				A_Explode(1, 70, 0);
			}
			FIR3 JKL 1 BRIGHT light ("BIGREDTORCH") {{doomb_flare_base.doomb_attach_flare(self, "RedFlare");}} 
			TNT1 A 0 A_Explode(3, 150, 0);
		Stop;
	
		Death:
			TNT1 A 0
			{
				A_Explode(3, 50);
				scale.x = frandom(1.3, 1.5);
			}
			FIR3 ABCDEFGHIJKLMNOP 1 BRIGHT {doomb_flare_base.doomb_attach_flare(self, "RedFlare");}
		Stop;
	}
}
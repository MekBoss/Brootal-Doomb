//===========================================================================
//
// Revenant
//
//===========================================================================
CLASS doomb_Revenant : doomb_monster_base
{ 
Default {
	
	doomb_monster_base.headshot_multiplier 2;
	doomb_monster_base.head_starting_position 50;//so that if someone forgot to define this number monster dont get headshot from every attack
	doomb_monster_base.legs_height_max 10;//same for legs
	speed 10;
	mass 600;
	MaxStepHeight 24;
	MaxDropOffHeight 32;
	Radius 20;
	Height 56;
	scale 0.9;
	GibHealth 45;
	Health 250;
	MeleeThreshold 196;
	
	
	Monster;
	+DONTHARMCLASS;
	+FLOORCLIP;
	damagefactor "Crush", 10.0;
	damagefactor "Blood", 0.0;
	damagefactor "BlueBlood", 0.0;
	damagefactor "GreenBlood", 0.0;
	damagefactor "Plasma", 0.5;
	PainChance "Kick", 255;
	
	MeleeSound "skeleton/melee";
	SeeSound "skeleton/sight";
	PainSound "REVOUCH";
	DeathSound "skeleton/death";
	ActiveSound "REVIDLE";

	BloodType "RevenantBlood";
	HitObituary "$OB_UNDEADHIT";
	PainChance "HelperMarineFatality", 255;
	Obituary "$OB_UNDEAD";
	DropItem "DemonStrengthRune", 5;
	species 'skeleton';
}
States
{
	Idle:
	LookingForPlayer:
		SKEL BBB 10 A_Look();
		TNT1 A 0 A_SetAngle(angle - 40);
		SKEL BBB 10 A_Look();
		TNT1 A 0 A_SetAngle(angle + 40);
		SKEL BBB 10 A_Look();
		
		SKEL AABBCCDD 2
		{
			A_Look();
			A_Wander();
			A_Recoil(-2);
		}
	loop;		
		
		
	ForgetTarget:
		TNT1 A 0 A_ClearTarget();
		Goto LookingForPlayer;
		
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_JumpIfInventory("Curbstomp_Marine", 1, "FatalityMarine");
	Spawn2:
		SKEL B 10 A_Look();
		Loop;
		
	See:
		TNT1 A 0 doomb_update_memory();
	LookingForPlayer:
		TNT1 A 0 A_JumpIfInventory("Curbstomp_Marine", 1, "FatalityMarine");
		SKEL AAABBBCCCDDD 2 A_Chase();
		TNT1 A 0 A_JumpIfInTargetInventory("IsInvisible", 1, "ForgetTarget");
		tnt1 a 0 doomb_monster_memory();
	Goto See;
		
		
	Melee:
		TNT1 A 0 doomb_update_memory();
		TNT1 A 0 A_JumpIfInventory("Curbstomp_Marine", 1, "FatalityMarine");
		SKEL G 1 A_FaceTarget();
		SKEL G 6 A_SkelWhoosh();
		SKEL H 6 A_FaceTarget();
		SKEL I 6
		{
			actor a = doomb_cheap_melee_attack(100, random(10, 60), "RVFT");
		
			if(a && a.health <= 0)
			{
				name type = a.getclassname();

				switch(type)
				{
					case 'doomb_zombie_Man':
						a.destroy();
						setstatelabel("FatalityZombieman");
					break;

					default: break;
				}
			}
		}

		TNT1 A 0 A_JumpIfInventory("SergeantFatality", 1, "FatalitySergeant");
		TNT1 A 0 A_JumpIfInventory("ImpFatality", 1, "FatalityImp");
	Goto See;
		
	Missile:
		TNT1 A 0 doomb_update_memory();
		TNT1 A 0 A_JumpIfInventory("Curbstomp_Marine", 1, "FatalityMarine");
		SKEL J 2 A_FaceTarget();
		TNT1 A 0 A_Jump (64, "SpecialAttack");
		SKEL J 1 BRIGHT A_FaceTarget();
		SKEL J 9 BRIGHT A_FaceTarget();
		SKEL K 10 A_SpawnProjectile("RevenantBalls",50,randompick(-10, 10),0,0);
	Goto See;
		
	SpecialAttack:
		TNT1 A 0 A_PlaySound("skeleton/sight");
		SKLA AB 3;
		SKLA CDCECDCE 3 A_FaceTarget();
		SKLA BA 3 A_FaceTarget();
		SKEL J 1 BRIGHT A_FaceTarget();
		SKEL J 11 BRIGHT A_FaceTarget();
		SKEL K 8 A_SpawnProjectile("RevenantMissile",60,-10,0,0);
		SKEL J 8 A_FaceTarget();
		SKEL K 8 A_SpawnProjectile("RevenantMissile",60,10,0,0);
		SKEL J 8 A_FaceTarget();
		SKEL K 8 A_SpawnProjectile("RevenantMissile",60,-10,0,0);
		SKEL J 8 A_FaceTarget();
		SKEL K 8 A_SpawnProjectile("RevenantMissile",60,10,0,0);
		SKEL J 8 A_FaceTarget();
	Goto See;

	Pain:
		TNT1 A 0 A_JumpIfInventory("Curbstomp_Marine", 1, "FatalityMarine");
		SKEL L 5;
		SKEL L 5 A_Pain();
		Goto See;
		
	Pain.Kick:
		TNT1 A 0 A_FaceTarget();
		TNT1 A 0 A_Recoil(6);
		SKEL L 6 A_Pain ();
		SKEL L 6;
	Goto See;
		
		
	FatalityMarine:
		TNT1 A 0 A_SetInvulnerable();
		TNT1 A 0 A_PlaySound("DSFDTHE");
		REFP A 30;
		EXPL A 0 Radius_Quake (2, 24, 0, 15, 0);
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		TNT1 A 0 doomb_much_blood();
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		REFP BCDE 8;
		REFP F 40;
		REFP GHI 4;
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		REFP J 20;
		TNT1 A 0 A_TakeInventory("Curbstomp_Marine",1);
		TNT1 A 0 A_UnSetInvulnerable();
		TNT1 A 0 doomb_severed_limb("MarineFatalizedByRevenant", limb_ignore_cvar: true);
	Goto See;
		
		
		
	FatalityZombieman:
		TNT1 A 0 A_SetInvulnerable();
		SKF1 A 1 A_Chase(null,null);
		SKF1 ABC 4 A_FaceTarget();
		TNT1 A 0 A_PlaySound("grunt/pain", 1);
		TNT1 A 0 A_PlaySound("CLAP", 4);
		TNT1 A 0 doomb_much_blood(0, 60);
		SKF1 D 12;
		
		SKF1 EFG 4 A_FaceTarget();
		TNT1 A 0 A_PlaySound("grunt/pain", 1);
		TNT1 A 0 A_PlaySound("CLAP", 4);
		TNT1 A 0 doomb_much_blood(0, -60);
		SKF1 H 12;
		tnt1 a 0 a_jump(200, "FatalityZombieman");

		SKF1 I 1 A_Chase(null,null);
		SKF1 I 10 A_FaceTarget();
		TNT1 A 0 A_UnSetInvulnerable();
		TNT1 A 0 {bSHOOTABLE = true;}
		TNT1 A 0 A_SpawnProjectile("ThrowedZombieMan2", 64, 0, 0, 0);
		SKEL HI 4;
	Goto See;
		
		
	FatalitySergeant:
		TNT1 A 0 A_SetInvulnerable();
		SKF2 A 1 A_Chase(null,null);
		SKF2 ABC 4 A_FaceTarget();
		TNT1 A 0 A_PlaySound("grunt/pain", 1);
		TNT1 A 0 A_PlaySound("CLAP", 4);
		TNT1 A 0 doomb_much_blood(0, 60);
		SKF2 D 12;
		
		SKF2 EFG 4 A_FaceTarget();
		TNT1 A 0 A_PlaySound("grunt/pain", 1);
		TNT1 A 0 A_PlaySound("CLAP", 4);
		TNT1 A 0 doomb_much_blood(0, -60);
		SKF2 H 12;
		tnt1 a 0 a_jump(200, "FatalitySergeant");

		SKF2 I 1 A_Chase(null,null);
		SKF2 I 10 A_FaceTarget();
		TNT1 A 0 A_TakeInventory("SergeantFatality",1);
		TNT1 A 0 A_UnSetInvulnerable();
		TNT1 A 0 A_SpawnProjectile("ThrowedSergeant2", 64, 0, 0, 0);
		SKEL HI 4;
	Goto See;
		
	FatalityImp:
		TNT1 A 0 A_SetInvulnerable();
		SKF3 A 1 A_Chase(null,null);
		SKF3 ABC 4 A_FaceTarget();
		TNT1 A 0 A_PlaySound("grunt/pain", 1);
		TNT1 A 0 A_PlaySound("CLAP", 4);
		TNT1 A 0 doomb_much_blood(0, 60);
		SKF3 D 12;
		
		SKF3 EFG 4 A_FaceTarget();
		TNT1 A 0 A_PlaySound("grunt/pain", 1);
		TNT1 A 0 A_PlaySound("CLAP", 4);
		TNT1 A 0 doomb_much_blood(0, -60);
		SKF3 H 12;
		tnt1 a 0 a_jump(200, "FatalityImp");
		
	throw_imp:
		SKF3 I 1 A_Chase(null,null);
		SKF3 I 10
		{
			A_FaceTarget();
			A_SpawnProjectile("ThrowedImp2", 64, 0, 0, 0);
			A_TakeInventory("ImpFatality",1);
			A_UnSetInvulnerable();
		}
		SKEL HI 4;
	Goto See;

	Death.Shotgun:
		TNT1 A 0 A_JumpIfCloser(100, "SpecialDeath");
	Death:
		SKEL LM 7;
		SKEL N 7 A_Scream();
		SKEL OP 7;
		TNT1 A 0 A_NoBlocking();
		TNT1 A 0 doomb_spawn_bloodpool();
		TNT1 a 0 doomb_severed_limb("DeadSkeleton", limb_ignore_cvar: true);
	Stop;
		
		
		
	Death.Headshot:
		TNT1 A 0 A_XScream();
		TNT1 A 0 A_Jump(126, "headshot_1");
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		TNT1 AAA 0 doomb_severed_limb("SmallLSPart1", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("SmallLSPart3", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("LSpart1", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("LSpart3", 'spawn', (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("LSpart2", 'spawn', (0, 0, 70), doomb_random_velocity(5) );
		REVH A 24;
		REVH B 8 A_NoBlocking();
		REVH CD 8;
		TNT1 A 0 doomb_spawn_bloodpool();
		TNT1 A 0 doomb_severed_limb("DeadSkeletonNoHead", limb_ignore_cvar: true );
	Stop;
		
	headshot_1:
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		TNT1 A 0 doomb_severed_limb("XDeathRevenantHead", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		REVH A 24;
		REVH B 8 A_NoBlocking();
		REVH CD 8;
		TNT1 A 0 doomb_spawn_bloodpool();
		TNT1 A 0 doomb_severed_limb("DeadSkeletonNoHead", limb_ignore_cvar: true );
	Stop;
		
		
	Death.Cutless:
		TNT1 A 0 A_Jump(100, "SpecialDeath");
	Goto Death;
		
	Death.Explosive_Impact:
		TNT1 A 0 A_JumpIfHealthLower(-40, "XDeath");
	Death.Cut:
	Death.Saw:
	Death.SSG:
	SpecialDeath:
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		TNT1 AAA 0 doomb_severed_limb("RevenantDust", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("RevenantDust2", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("BigLSPart1", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("BigLSPart2", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAAAAAAAAA 0 doomb_severed_limb("RevenantPiece", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 A 0 A_Jump(64, "Death.Rare");
		TNT1 A 0 A_NoBlocking();
		TNT1 A 0 A_Scream();
		SKEX BCDE 6;
		TNT1 A 0 doomb_severed_limb("XDeadSkeleton", limb_ignore_cvar: true );
	Stop;
		
	Death.Plasma:
	Death.Plasma2:
		TNT1 A 0;
		TNT1 A 0 A_Scream();
		TNT1 A 0 A_NoBlocking();
		TNT1 AAA 0 doomb_for_khorne("doomb_non_interactive_blood", "red_blood_trail", (0, 0, random(20, 30) ), (frandom(-5, 5), frandom(-5, 5), frandom(0, 5) ) );
		TNT1 AAA 0 doomb_severed_limb("XDeathBurnedSkull", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		EXPL AAAA 0 doomb_show_effect("ExplosionSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 1) ) );
		RVDP ABCDEFG 5 BRIGHT {doomb_flare_base.doomb_attach_flare(self, "BlueFlare", (0, 0, 33) );}
		RVDP GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 10 doomb_show_effect("AltSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ), (1.5, 0) );
		RVDP G -1;
	Stop;
		
	Death.Fire:
		SKEL LM 4;
		SKEL N 4 A_Scream();
		SKEL O 4 A_NoBlocking();
		SKEL P 1;
		TNT1 A 0 A_Playsound("props/redfire");
		CARB NNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNN 2 doomb_show_effect("FlameTrails", (0, 0, 25), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ) );
		CARB NNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNN 10 doomb_show_effect("ExplosionSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ), (0.5, 0), 0.1 );
		CARB N -1;
		Stop;
		
	XDeath:
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		TNT1 AAA 0 doomb_severed_limb("RevenantDust", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("RevenantDust2", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("BigLSPart1", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("BigLSPart2", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 A 0 doomb_severed_limb("XDeathRevenantHead", "spawn", (0, 0, 70), doomb_random_velocity(5) );

		TNT1 AAA 0 doomb_severed_limb("RevenantArm", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("RevenantTorso5", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("MetalShard1", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("MetalShard2", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("MetalShard3", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("RevenantBonesFast", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 AAA 0 doomb_severed_limb("RevenantPiece", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		TNT1 A 0 A_SpawnItem("HellishMissileLauncher");
		TNT1 A 0 A_NoBlocking();
		TNT1 A 0 A_Scream();
	Stop;
		
	Death.Rare:
		TNT1 A 0 A_NoBlocking();
		TNT1 A 0 A_Scream();
		TNT1 A 0 doomb_severed_limb("RevenantU", "spawn", (0, 0, 40), doomb_random_velocity(5), true );
		TNT1 A 0 doomb_severed_limb("RoamingLegs", limb_ignore_cvar: true);
	Stop;

	Pain.Taunt:
		TNT1 A 0;
	Goto Missile;





	Death.Fatality:
		tnt1 A 0
		{
			doomb_monster_fatality_camera(target);
			A_Pain();
			A_NoBlocking();
		}
		TNT1 A 0 A_Jump(256, "revenant_fatality_1", "revenant_fatality_2");

	revenant_fatality_1:
		FREV ABABAB 3;
		tnt1 a 0 Radius_Quake (4, 24, 0, 15, 0);
		tnt1 AAAAAA 0 doomb_sticky_guro(SGT_BLOOD);
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		tnt1 AAAAAAAAAAAAAAAAAAAA 0 doomb_for_khorne("PlayerFlyingBlood", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		tnt1 A 0 ACS_Execute(580, 0, 0, 0, 0);
		tnt1 A 0 ACS_Execute(581, 0, 0, 0, 0);
		tnt1 A 0 ACS_Execute(582, 0, 0, 0, 0);
		FREV C 4 A_PlaySound("skeleton/pain");
		FREV D 4;
		FREV EFGHIJJJKL 4;
		FREV M 5 A_PlaySound("skeleton/sight");
		FREV M 10;
		FREV NO 2;
		
		tnt1 a 0 Radius_Quake (4, 24, 0, 15, 0);
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		TNT1 AAA 0 doomb_severed_limb("XDeathRevenantHeadKicked2", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		tnt1 A 0 ACS_Execute(580, 0, 0, 0, 0);
		tnt1 A 0 ACS_Execute(581, 0, 0, 0, 0);
		tnt1 A 0 ACS_Execute(582, 0, 0, 0, 0);
		tnt1 A 0 ACS_Execute(583, 0, 0, 0, 0);
		
		FREV PQR 2;
		tnt1 A 0 
		{
			A_Givetotarget("HealthBonus",15);
			doomb_severed_limb("DyingRevenant", limb_ignore_cvar: true);
			doomb_destroy_monster_fatality_camera();
		}
	stop;
	
	
	revenant_fatality_2:
		FRE2 A 5;
		FRE2 A 5 A_PlaySound("skeleton/pain");
		FRE2 A 5;
		tnt1 a 0 Radius_Quake (4, 24, 0, 15, 0);
		tnt1 AAAAAA 0 doomb_sticky_guro(SGT_BLOOD);
		TNT1 A 0 doomb_spawn_entrails( (0, 0, random(30, 35) ) );
		tnt1 AAAAAAAAAAAAAAAAAAAA 0 doomb_for_khorne("PlayerFlyingBlood", "spawn", (0, 0, 70), doomb_random_velocity(5) );
		FRE2 BCDEFGGGGG 6;
		tnt1 A 0
		{
			A_Givetotarget("HealthBonus",15);
			doomb_severed_limb("VeryFuckedUpRevenant", limb_ignore_cvar: true);
			doomb_destroy_monster_fatality_camera();
		}
	stop;
}
}

CLASS RevenantBalls : doomb_boolet_base
{
Default
{
	Radius 10;
	Height 4;
	Speed 14;
	FastSpeed 18;
	Damagefunction (random(15,20));
	PROJECTILE;
	+THRUGHOST;
	+SEEKERMISSILE;
	Scale 1.0;
	damagetype "bLAST";
	SeeSound "skeleton/attack";
	DeathSound "skeleton/tracex";
}
States
{
	Spawn:
		FATB AB 1 BRIGHT light ("ROCKETLIGHT")
		{
			{doomb_flare_base.doomb_attach_flare(self, "RedFlareSmall", visual_size: 0.15);}
			if(waterlevel > 1)
				A_SpawnProjectile ("BUBULZ", 0, 0, random (0, 360), 2, random (0, 180));
			
			else
			{
				doomb_show_effect("FlameTrails", visual_size:(0.1, 0) );
				doomb_show_effect("ExplosionSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ), (0.3,0), 0.3 );
			}
			A_SeekerMissile (4,8);
		}
	Loop;
		
		
	Death:
		EXPL A 0 Radius_Quake (2, 54, 0, 15, 0);
		TNT1 A 0 A_SpawnItem("WhiteShockwaveSmall");
		TNT1 A 0 {doomb_flare_base.doomb_attach_flare(self, "NewfagExplosionFlare");}
		TNT1 A 0 {if(waterlevel > 1) spawn("UnderwaterExplosion", pos:self.pos); }
		tnt1 A 0 A_Explode(7, 120, 1);
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 doomb_show_effect("ExplosionParticle2", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ) );
		TNT1 A 0 doomb_show_effect("ExplosionFlames", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5) ), (frandom(0.9, 1.4), 0) );
		TNT1 A 1;
	Stop;
}

}

CLASS RevenantMissile: RevenantBalls { 
States
{
	Spawn:
		FATB AB 1 BRIGHT  {doomb_flare_base.doomb_attach_flare(self, "YellowFlare", visual_size:0.15);}
		TNT1 A 0 doomb_show_effect("FlameTrails", visual_size:(0.1, 0) );
		TNT1 A 0 doomb_show_effect("ExplosionSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ), (0.3,0), 0.3 );
		Loop;
}

}
class doomb_monster_base : actor abstract
{
//if you have a feeling that zscripted project brutality and zscripted brutal doom share similar code that not a feeling

//link to event which control effects
protected eventhandler sfx;
void set_effects(eventhandler effects)
{
    if(effects.getclassname() == "doomb_sfx_main")
	    sfx = effects;
}
protected visual_sfx doomb_show_effect(statelabel effect_name, vector3 offset = (0, 0, 0), vector3 velocity = (0, 0, 0), vector2 visual_size = (double.nan, double.nan), double translucency = double.nan)
{
	if(sfx)
		return doomb_sfx_main(sfx).show_some_effect(self, effect_name, offset, velocity, visual_size, translucency);
	
	else
		return null;
}



//handler for freeze/fatality/dismembering and other death types
//where monster ripped to pieces
protected bool frozen, dismembered, cant_resurrect;

override bool CanResurrect(Actor other, bool passive)
{
	if (!passive && self.getclassname() == "TehArchvile")
	{
		return false;
	}
	return !cant_resurrect;
}

//head position is height on which "body" of monster should become "head"
//I recomend to set head_starting_position approx. to "monster heght" - "monster height / 10"
//because golden ratio and it dont looks stupid in game
protected double head_mul, head_pos, legs_pos;

Property headshot_multiplier : head_mul;
Property head_starting_position : head_pos;

//for legs, height where legs lost it preciese name and turns into ass
Property legs_height_max : legs_pos;

//references to gore function so it can decide what blood and how much spawn
//blood color obvious, blood size is amount of a blood/gore it must spawn(realistic/japanes cartoon/etc)
//not confuse with cvar
protected string blood_color, blood_size;

//blood color for translation, by default are red, can be or green or blue
Property blood_color : blood_color;
Property blood_size : blood_size;


//handling of ammo and weapon spawning after monster death
protected class<actor> weapon_type;

Property weapon_type : weapon_type;

//true weapon, false ammo
protected bool chose_what_spawn()
{
	if(random(0, 10) > 7) {return true;}
	return false;
}

protected void spawn_reward()
{
	if(weapon_type != null && !(self is "doomb_body_base") )
	{
		if( chose_what_spawn() )
		{
			spawn(weapon_type, self.pos + (0, 0, default.height/2) ).vel.z = 5;
		}
		else
		{
			class<Ammo> amo = weapon(getdefaultbytype(weapon_type) ).AmmoType1;
			if(amo == null) return;
			inventory am = (inventory)(spawn(amo, pos + (0, 0, default.height/2) ) );
			if(random(0, 1) == 1) inventory(am).amount += random(0, inventory(am).amount);
		}
	}
}

protected bool called_die;

override void Die(Actor source, Actor inflictor, int dmgflags, Name MeansOfDeath)
{
	if(!called_die)
	{
		spawn_reward();
		called_die = true;
	}
	
	super.Die(source, inflictor, dmgflags, MeansOfDeath);
}
/*
override void OnDestroy()
{
	if(!called_die)
	{
		spawn_reward();
		called_die = true;
	}

	super.ondestroy();
}
*/

protected void doomb_if_boss_retreat(actor who = null, statelabel retereat_to = null)
{
	if(who && retereat_to)
	{
		if(who.bboss)
			self.setstatelabel(retereat_to);
	}
}


default
{
	doomb_monster_base.headshot_multiplier 1;
	doomb_monster_base.head_starting_position -1;//so that if someone forgot to define this number monster dont get headshot from every attack
	doomb_monster_base.legs_height_max -1;//same for legs
	monster;
	
	doomb_monster_base.blood_color "";//by default it "null", empty string
	doomb_monster_base.blood_size "";//same for size

	doomb_monster_base.weapon_type '';//weapon type must be defined in 'final' monster definition
}

protected bool is_headshot(vector3 v)
{
	//if head variable valid
	if(doomb_monster_base(self).head_pos > -1 && v.z >= doomb_monster_base(self).head_pos)
		return true;

	return false;
}

protected bool is_kneeshot(vector3 v)
{
	//if leg variable valid
	if(doomb_monster_base(self).legs_pos > -1 && v.z <= doomb_monster_base(self).legs_pos)
		return true;

	return false;
}

override int DamageMobj(Actor inflictor, Actor source,int damage,Name mod,int Flags,Double angle)
{	//check if monster actually alive
	if(self.bkilled == false)
	{
        //explosion cant inflict headshot
		if(!(flags & DMG_EXPLOSION) && inflictor)
		{
			//to handle cases when actor vulnerable/invulnerable to some damage type
			//before apply headshot multiplicator
			int new_damage = damage;
		
			vector3 relat_pos = self.vec3to(inflictor);

			if(is_headshot(relat_pos) )
			{
				new_damage *= (int)(self.head_mul);
				return super.DamageMobj(inflictor, source, new_damage, 'headshot', flags ,angle);
			}

            //check for arrow in the knee
			if(is_kneeshot(relat_pos))
			{
				return super.DamageMobj(inflictor, source, new_damage, 'leg', flags ,angle);
			}

			if( (source is "Doomer") )
			{
				if(source.player.readyweapon.getclassname() == "Melee_Attacks" )
				{
					//backstab
					if(self.LastHeard == null && self.lastenemy == null)
					{
							return super.DamageMobj(inflictor, source, self.health, 'back_stab', flags, angle);
					}
					//default fatality
					else if(findstate("Death.Fatality") && (health - new_damage) < 0 )
						return super.DamageMobj(inflictor, source, damage, 'Fatality', flags, angle);

				}
			}

			return super.DamageMobj(inflictor, source, damage, mod, flags ,angle);
		}

        //its explosion
		return super.DamageMobj(inflictor, source, damage, mod, flags, angle);
	}

    //case when monster already dead, but technicallly still can be shoot
	else
	{
		int dam = super.DamageMobj(inflictor, source, damage, mod, flags, angle);

		if(health - dam <= 0)
			setstatelabel("xdeath");

		return dam;
	}
}
//can actually hit another actor(not monster target)
protected void doomb_cheap_melee_attack(double distance = 0, int dam = 0, name damage_type = "")
{
	double new_pitch = AimLineAttack (angle, DEFMELEERANGE, null, 0., ALF_CHECK3D);
	FLineTraceData punch;
	LineTrace(angle + frandom(-5, 5), distance, new_pitch + random (-5, 5),
	offsetz:height / 2, data: punch);
	
	if(punch.HitType == TRACE_HitActor)
	{
		//console.printf("cheap melee hit %s", punch.hitactor.getclassname() );
		punch.hitactor.damagemobj(self, self, dam, damage_type, 0, 0);
	}

}

//handling of freeze death
override double GetDeathHeight()
{
	if(DamageType == 'Ice' || DamageType == 'Freeze' || DamageType == 'Chill')
		return default.height;

	return super.GetDeathHeight();
}

//how much time monster seeek it target before forgeting
protected int memory_timer;
const max_memory = 20;
protected void doomb_monster_memory(statelabel idle_state = "idle")
{
	memory_timer++;
	if(memory_timer > max_memory)
	{
		memory_timer = 0;
		a_cleartarget();
		state jump_to = findstate(idle_state);
		if(jump_to == null)
			jump_to = findstate("see");

		setstate(jump_to);
	}
}


protected double prev_z_vel;

protected bool check_falling()
{
	if(vel.z == 0 && prev_z_vel < 0)
		return true;
	
	prev_z_vel = vel.z;
	return false;
}

protected void gibbed_falling()
{
	if(check_falling() ) {}
	else{setstate(curstate);}
}

states
{
	bug:
		"####" "#" -1
		{
			console.printf("actor %s at %F %F %F  miss state changing ", getclassname(), pos.x, pos.y, pos.z);
		}
	stop;

	//kill monsters and mdk cheats damage type
	//so it wont explode computer by spawning >9000 blood actors
	Death.Massacre:
		tnt1 a 1;
	stop;

	Death.Ice:
	Death.Freeze:
	Death.Frost:
		"####" "#" 0
		{
			self.bSHOOTABLE = false; self.bSOLID = false;
			A_SetTranslation("ice");
			cant_resurrect = true;
		}
		"####" "#" 1 {tics = random(350, 700);}
	
		"####" "#" 5
		{
			if(scale.x > 0.01)
			{
				scale -= (0.01, 0.01);
				//spawn smoke here
				//setstate(curstate);
			}
		}
	Stop;

	death.desintegrate:
        TNT1 A 0 A_XScream;
        TNT1 A 0 A_NoBlocking;
		TNT1 AAAAAAAA 0 A_SpawnProjectile ("Brutal_FlyingBloodFake", 30, 0, random (0, 360), 2, random (0, 90));
		TNT1 AAAA 0 A_SpawnProjectile ("Brutal_FlyingBlood", 37, 0, random (0, 360), 2, random (10, 50));
		TNT1 A 0 A_SpawnProjectile ("Brutal_FlyingBlood", 37, 0, random (0, 360), 2, random (40, 60));
		TNT1 AAA 0 A_SpawnProjectile ("BloodmistSmall", 37, 0, random (0, 360), 2, random (30, 90));
		TNT1 AA 0 A_SpawnProjectile ("XDeath1", 30, 0, random (0, 360), 2, random (0, 90));
		TNT1 AAA 0 A_SpawnProjectile ("XDeath2", 30, 0, random (0, 360), 2, random (0, 90));
		TNT1 AAA 0 A_SpawnProjectile ("XDeath3", 30, 0, random (0, 360), 2, random (0, 90));
		TNT1 AAAAA 0 A_SpawnProjectile ("Instestin", 24, 0, random (0, 360), 2, random (0, 160));
		TNT1 A 0 doomb_spawn_bloodpool();
		TNT1 A 0 A_SpawnProjectile ("SuperGoreSpawner", 30, 0, random (0, 360), 2, random (10, 90));
		ID50 ABCDEF 7;
		ID50 GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 13 doomb_show_effect("ExplosionSmoke", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ), (0.5, 0), 0.1 );
		ID50 G -1;
    Stop;

	death.taunt:
	death.avoid:
		tnt1 a 0
		{
			console.printf("taunt/avoid death type");
			setstatelabel("see");
		}
	goto bug;

	Raise:
		TNT1 A 2 doomb_show_effect("RealFlameTrailsSmall", (0, 0, 5), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ) );
		TNT1 A 0 A_SpawnItem("PentagramSpawner", 0, 60);
		TNT1 A 2 doomb_show_effect("RealFlameTrailsSmall", (0, 0, 5), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.5, 1) ) );
		TNT1 A 0 A_SpawnItem("PentagramSpawner", 0, 60);
		TNT1 A 0 A_SpawnItem("TeleportFog");
		TNT1 A 0 setstatelabel("see");
	goto bug;

}


protected uint limb_state;
//
protected void doomb_final_absolution(int h, int r)
{
	health += 200;
	self.bDONTTHRUST = true;
	self.bNOPAIN = true;
	self.bNODROPOFF = false;
	self.bSLIDESONWALLS = true;
	self.bsolid = false;
	self.bcorpse = true;
	self.bshootable = true;
	a_setsize(h, r);

	limb_state |= DLF_FINAL_ABSOLUTION;
	cant_resurrect = true;
}

override void tick()
{
	super.tick();

	if(limb_state &= DLF_FINAL_ABSOLUTION)
	{
		if(health <= 0)
		{
			spawn("XDeath1", pos:self.pos + (0, 0, 7)).vel = (random(-3, 3), random(-3, 3), random(-3, 3) );
			spawn("XDeath2", pos:self.pos + (0, 0, 7)).vel = (random(-3, 3), random(-3, 3), random(-3, 3) );
			spawn("XDeath3", pos:self.pos + (0, 0, 7)).vel = (random(-3, 3), random(-3, 3), random(-3, 3) );
			spawn("XDeath4", pos:self.pos + (0, 0, 7)).vel = (random(-3, 3), random(-3, 3), random(-3, 3) );

			self.destroy();
		}
	}

	if(curstate == findstate("missile") || curstate == findstate("melee"))
		memory_timer = 0;

}






protected void doomb_spawn_big_blood(int spawn_height = 0)
{
	if(waterlevel < 1)
    {
		for(uint i = 0; i < 2 * bd_bloodamount; i++)
		{
			actor a = A_SpawnProjectile ("Brutal_FlyingBloodTrail", 0, 0, random (0, 180), 2, random (0, 180) );
			a.setorigin(self.pos + (0, 0, spawn_height), false);

			if( (i % 2) == 0)
			{
				a = A_SpawnProjectile ("Brutal_FlyingBloodTrail", 15, 0, random (0, 360), 2, random (0, 180) );
				a.setorigin(self.pos + (0, 0, spawn_height), false);
			}
		}
	}
}

protected void doomb_spawn_burned_flesh()
{
	A_SpawnProjectile ("XDeathBurnedMeat3", 32, 0, random (0, 360), 2, - random (0, 160));
	A_SpawnProjectile ("XDeathBurnedMeat2", 32, 0, random (0, 360), 2, - random (0, 160));
	A_SpawnProjectile ("XDeathBurnedMeat", 32, 0, random (0, 360), 2, - random (0, 160));
}

protected void doomb_spawn_bloodpool()
{
	if(GetFloorTerrain().isLiquid == false)
	{
		actor a = spawn("doomb_red_blood_pool", pos:self.pos);

		if(a)
		{
			if(self.getclassname() == "doomb_cacodemon")
			{
				a.A_SetTranslation("red_to_blue");
				doomb_red_blood_pool(a).color = "blue";
			}

			if(self.getclassname() == "doomb_baron" || self.getclassname() == "doomb_knight")
			{
				a.A_SetTranslation("red_to_green");
				doomb_red_blood_pool(a).color = "green";
			}
		}
	}
}

}


/*
|= for turn on
&= ~"flag name" for turn off
&= for comparing

*/
enum doomb_monsters_limb_states
{
	DLF_NO_HEAD = 1,//1

	DLF_NO_RIGHT_HAND = 1<<1,//2 -> 10
	DLF_NO_LEFT_HAND = 1<<2,//4 -> 100
	
	DLF_NO_RIGHT_LEG = 1<<3,//8 -> 1000
	DLF_NO_LEFT_LEG = 1<<4,//and so on

	DLF_FINAL_ABSOLUTION = 1<<10//
};

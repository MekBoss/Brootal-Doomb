//Tracing projectiles goes here
//What is "tracing projectiles"?
CLASS doomb_model_boolet : doomb_boolet_base
{
	Default
	{
		Projectile;
		+RANDOMIZE;
		+FORCEXYBILLBOARD;
		+DONTSPLASH;
		+NOEXTREMEDEATH;
		-DONTSPLASH;
		+BLOODSPLATTER;
		+THRUGHOST;

		damagefunction ( doomb_boolet_damage() );
		radius 2;
		height 2;
		speed 60;
		renderstyle "Add";
		alpha 0.9;
		scale 0.15;
		Decal "BulletDecalNew1";
	}

	private int lower_damage_bound, upper_damage_bound;

	void doomb_set_boolet_damage(int i, int j)
	{
		lower_damage_bound = i;
		upper_damage_bound = j;
	}

	private int doomb_boolet_damage()
	{
		return random(lower_damage_bound, upper_damage_bound);
	}

	states
	{
		Spawn:
			TRAC A 1 BRIGHT nodelay
			{
				if(target is 'doomb_mind')
				{
					scale = (0.2, 0.2);
				}
				vel *= frandom(0.8, 1.4);
				bmissile = true;
			}
		spawn_mini_loop:
			TRAC A 1 BRIGHT;
		Loop;

		Death:
			tnt1 a 1
			{
				if(!blockingmobj)
					spawn("RicoChet", pos);
				
				if(target is 'doomb_mind')
				{
					A_Explode (2, 120, fulldamagedistance: 120);
					doomb_flare_base.doomb_attach_flare(self, "NewfagExplosionFlare");
					doomb_show_effect("ExplosionParticle2", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5) ) );
					Radius_Quake (4, 24, 0, 15, 0);
					doomb_show_effect("ExplosionFlames", (0, 0, 0), (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5) ), (0.9, 0.9) );
					A_SpawnItem("WhiteShockwaveSmall");
					A_PlaySound("FAREXPL");
				}
			}
		Stop;
	}
}

//players bullets
CLASS MinigunTracer: doomb_model_boolet { 
	Default {
		seesound "weapons/minigunfire";
		damagetype "mini_gun";
	}
}